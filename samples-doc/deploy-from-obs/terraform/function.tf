##########################################################
# Create python event function
##########################################################
resource "opentelekomcloud_fgs_function_v2" "MyFunction" {
  
  name = format("%s_%s", var.prefix, var.function_name)
  app  = "default"

  handler = var.handler_name

  initializer_handler = var.initializer_name
  initializer_timeout = 30

  runtime = "Python3.10"

  ###### relevant part for deploy function code from obs file ######
  code_type = "obs"
  code_url = format("https://%s/%s/%s",
    opentelekomcloud_obs_bucket.codebucket.bucket_domain_name,
    "code",
    basename(var.zip_file_name)
  )
  # on change of the code object etag (hash) new code  version will be deployed.
  source_code_hash = opentelekomcloud_obs_bucket_object.code_object.etag
  ###### ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ ######

  description      = var.description
  memory_size      = 512
  timeout          = 30
  max_instance_num = 1

  log_group_id   = opentelekomcloud_lts_group_v2.MyLogGroup.id
  log_group_name = opentelekomcloud_lts_group_v2.MyLogGroup.group_name

  log_topic_id   = opentelekomcloud_lts_stream_v2.MyLogStream.id
  log_topic_name = opentelekomcloud_lts_stream_v2.MyLogStream.stream_name

  # set some environment variables
  user_data = jsonencode({
    "RUNTIME_LOG_LEVEL" : "DEBUG",
  })

  tags = {
    "app_group" = var.tag_app_group
  }

}

output "MY_FUNCTION_URN" {
  value = opentelekomcloud_fgs_function_v2.MyFunction.urn
}

output "MY_FUNCTION_VERSION" {
  value = opentelekomcloud_fgs_function_v2.MyFunction.version
}
